/*
 * To deploy on Bintray you need to set 'bintrayUser' and 'bintrayKey' in
 * '~/.gradle/gradle.properties'.
 *
 * You should also set 'bintrayUrl' in the project's 'gradle.properties' file.
 *
 * NOTE: For the package to be accepted in JCenter, you need to include sources (sourceJar task).
 */
buildscript {
    repositories {
        jcenter ()
        mavenCentral ()
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.github.zafarkhaja:java-semver:$semverVersion"
    }
}

plugins {
    id 'org.ajoberstar.grgit' version '1.6.0'
    id 'org.ajoberstar.github-pages' version '1.6.0'
    id 'org.xbib.gradle.plugin.jbake' version '1.2.1'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'uk.co.cacoethes.lazybones-templates' version '1.2.3'
}

apply from: 'gradle/site.gradle'
apply from: 'gradle/kotlin.gradle'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

ext.bintrayUser = findProperty ('bintrayUser') ?: 'anonymous'
ext.bintrayKey = findProperty ("${project.name}BintrayKey") ?: 'anonymous'

dependencies {
    // COMPILE
    compile ("ch.qos.logback:logback-classic:$logbackVersion")
    compile ("org.slf4j:jcl-over-slf4j:$slf4jVersion")
    compile ("org.slf4j:jul-to-slf4j:$slf4jVersion")
    compile ("com.cronutils:cron-utils:$cronutilsVersion") { exclude module: "slf4j-api" }

    compileOnly ("com.rabbitmq:amqp-client:$rabbitVersion")
    compile ("org.mongodb:mongodb-driver:$mongodbVersion") // TODO Set as 'compileOnly'

    compile ("org.asynchttpclient:async-http-client:$asynchttpclientVersion")
    compileOnly ("org.eclipse.jetty:jetty-webapp:$jettyVersion") { exclude module: "slf4j-api" }
    compileOnly ("io.undertow:undertow-core:$undertowVersion")
    compileOnly ("io.undertow:undertow-servlet:$undertowVersion")

    compile ("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion")
    compile ("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion")
    compile ("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion")
    compile ("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion") {
        exclude module: "kotlin-reflect"
    }

    compileOnly ("com.mitchellbosecke:pebble:$pebbleVersion")
    compileOnly ("org.jetbrains.kotlinx:kotlinx.html.jvm:$kotlinxHtmlVersion")

    // TEST
    testCompile ("org.testng:testng:$testngVersion")
    testCompile ("org.mockito:mockito-core:$mockitoVersion")
    testCompile ("com.rabbitmq:amqp-client:$rabbitVersion")
    testCompile ("org.eclipse.jetty:jetty-webapp:$jettyVersion") { exclude module: "slf4j-api" }
    testCompile ("com.mitchellbosecke:pebble:$pebbleVersion")
    testCompile ("io.undertow:undertow-core:$undertowVersion")
    testCompile ("org.jetbrains.kotlinx:kotlinx.html.jvm:$kotlinxHtmlVersion")
    testCompile ('com.zaxxer:HikariCP:2.5.1')
    testCompile ('mysql:mysql-connector-java:6.0.5')
    testCompile ("org.apache.qpid:qpid-broker:0.28") { // Version *MUST BE* 0.28 for tests to work
        exclude module: "commons-logging"
        exclude module: "slf4j-log4j12"
        exclude module: "slf4j-api"
    }
}

test {
    useTestNG ()

    systemProperties['derby.stream.error.file'] = '/dev/null'
    doFirst {
        File environmentFile = file("${System.getProperty("user.home")}/.environment")
        if (!environmentFile.exists())
            environmentFile.write("DEVELOPMENT\n")
    }
}

configurations {
    deployerJars
}

task sourceJar (type: Jar) {
    from sourceSets.main.allJava
}

task packageTest (type: Jar) {
    from sourceSets.test.output

    exclude '/data', '/public', '/templates', '/*.yaml', '/*.xml', '/*.json', '/*.txt'
}

publishing {
    publications {
        mavenJava (MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            artifact packageTest {
                classifier 'test'
            }
        }
    }
}

githubPages {
    pages {
        from "$buildDir/site"
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = [ 'mavenJava' ]
    publish = true

    pkg {
        repo = 'maven'
        name = 'Hexagon'
        licenses = [ 'MIT' ]
        vcsUrl = 'https://github.com/jaguililla/hexagon.git'
    }
}

lazybones {
    repositoryName = "$bintrayUser/maven/Hexagon"

    repositoryUsername = bintrayUser
    repositoryApiKey = bintrayKey

    licenses = [ 'MIT' ]
}

task libraries(dependsOn: [ 'build', 'packageTest' ], type: Copy) {
    into "$buildDir/libs"
    from configurations.runtime
}

site.dependsOn 'dokka', 'jacocoTestReport'
publishGhPages.dependsOn 'site'
bintrayUpload.dependsOn 'build'
publish.dependsOn 'publishGhPages', 'bintrayUpload' // TODO Add publishAllTemplates

task release(dependsOn: [ 'checkGitStatus', 'publish', 'libraries', 'installAllTemplates' ]) {
    doLast {
        grgit.tag.add {
            name = version
            message = "Release $version"
        }

        grgit.push()
    }
}

task publishLocal (dependsOn: [ 'build', 'publishToMavenLocal' ])

task checkGitStatus () {
    doLast {
        checkCleanRepository()
        grgit.pull (remote: 'origin', branch: 'master')
        checkCleanRepository()
    }
}

task major { doLast { version "major" } }
task minor { doLast { version "minor" } }
task patch { doLast { version "patch" } }

import static com.github.zafarkhaja.semver.Version.valueOf as versionOf

void checkCleanRepository() {
    if (!grgit.status ().clean)
        throw new GradleException ('Repository not clean')
}

void version(String type) {
    switch (type) {
        case "major":
            version = versionOf(version.toString ()).incrementMajorVersion ()
            break
        case "minor":
            version = versionOf(version.toString ()).incrementMinorVersion ()
            break
        case "patch":
            version = versionOf(version.toString ()).incrementPatchVersion ()
            break
        default:
            throw new RuntimeException ("Unsupported version change: $type")
    }

    File properties = file ("$rootDir/gradle.properties")
    String version = "version=${version.toString()}"
    properties.write(properties.getText().replaceAll(/(?m)version=.*/, version))

    // TODO Commit change
}
