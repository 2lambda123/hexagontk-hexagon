/*
 * Check usage information at: http://hexagonkt.com/gradle/#dokka
 */

apply(plugin: "kotlin")
apply(plugin: "maven-publish")
apply(plugin: "org.jetbrains.dokka")

task("dokkaJar", type: Jar, dependsOn: "dokkaHtml") {
    group = "distribution"
    description = "Create a jar file with the source code documentation in Javadoc format."

    archiveClassifier.set("javadoc")
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(fileTree("$buildDir/dokka/html"))
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(dokkaJar)
        }
    }
}

setUpDokka(dokkaHtml)
setUpDokka(dokkaGfm)
task("dokka", dependsOn: [ "dokkaHtml", "dokkaGfm" ])

private void setUpDokka(final Object dokkaTask) {
    dokkaTask.dokkaSourceSets {
        configureEach {
            reportUndocumented.set(false)
            includes.from(fileTree(projectDir) { include("*.md") })
            samples.from(fileTree(projectDir) { include("**/*SamplesTest.kt") })
            sourceRoots.from(file("src/main/kotlin"), file("src/test/kotlin"))
        }
    }
}
