/*
 * Check usage information at: http://hexagonkt.com/gradle/#dokka
 */

apply(plugin: "kotlin")
apply(plugin: "maven-publish")
apply(plugin: "org.jetbrains.dokka")

task("dokkaJar", type: Jar, dependsOn: "dokkaJavadoc") {
    group = "distribution"
    description = "Create a jar file with the source code documentation in Javadoc format."

    archiveClassifier.set("javadoc")
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(fileTree("$buildDir/dokka/javadoc"))
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(dokkaJar)
        }
    }
}

setUpDokka(dokkaJavadoc)
setUpDokka(dokkaGfm)
task("dokka", dependsOn: [ "dokkaJavadoc", "dokkaGfm" ])

private void setUpDokka(final Object dokkaTask) {
    dokkaTask.dokkaSourceSets {
        configureEach {
            reportUndocumented.set(false)
            includes.from(fileTree(projectDir) { include("*.md") })
            samples.from(fileTree(projectDir) { include("**/*SamplesTest.kt") })
            sourceRoots.from(file("src/main/kotlin"), file("src/test/kotlin"))
        }
    }
}
