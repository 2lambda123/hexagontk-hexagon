/*
 * Check usage information at: http://hexagontk.com/gradle/#publish
 */

apply(plugin: "signing")
apply(plugin: "maven-publish")
apply(plugin: "org.jreleaser")

signing {
    String signingKey = findProperty("signingKey") ?: System.getenv("SIGNING_KEY")
    String signingPassword = findProperty("signingPassword") ?: System.getenv("SIGNING_PASSWORD")

    useInMemoryPgpKeys(signingKey, signingPassword)
    required { gradle.taskGraph.hasTask("publish") }
    sign(publishing.publications)
}

publishing {
    repositories {
        maven {
            String stagingDirectory =
                findProperty("stagingDirectory") ?: System.getenv("STAGING_DIRECTORY")

            if (stagingDirectory == null) {
                url = "https://central.sonatype.com"

                credentials {
                    username = findProperty("ossrhUsername") ?: System.getenv("REPOSITORY_USERNAME")
                    password = findProperty("ossrhPassword") ?: System.getenv("REPOSITORY_PASSWORD")
                }
            }
            else {
                url = project.file(stagingDirectory)
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            afterEvaluate {
                pom {
                    name = project.name
                    description = project.description
                    url = findProperty("siteHost")

                    scm {
                        connection = "scm:git:$vcsUrl"
                        developerConnection = "scm:git:git@github.com:hexagontk/hexagon.git"
                        url = "https://github.com/hexagontk/hexagon"
                    }

                    licenses {
                        license {
                            name = project.findProperty("licenses")
                            url = "https://opensource.org/licenses/MIT"
                        }
                    }

                    developers {
                        developer {
                            id = "jaguililla"
                            name = "Juanjo Aguililla"
                            email = "jaguililla@hexagontk.com"
                        }
                    }
                }
            }
        }
    }
}

jreleaser {
    gitRootSearch = true

//    signing {
//        active = "ALWAYS"
//        armored = true
//        publicKey = findProperty("signingKey") ?: System.getenv("SIGNING_KEY")
//        secretKey = findProperty("signingKey") ?: System.getenv("SIGNING_KEY")
//        passphrase = findProperty("signingPassword") ?: System.getenv("SIGNING_PASSWORD")
//    }

    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = "ALWAYS"
                    url = "https://central.sonatype.com/api/v1/publisher"
                    username =
                        findProperty("REPOSITORY_USERNAME") ?: System.getenv("REPOSITORY_USERNAME")
                    password =
                        findProperty("REPOSITORY_PASSWORD") ?: System.getenv("REPOSITORY_PASSWORD")
                    stagingRepository("build/staging")
                }
            }
        }
    }
}
