
apply plugin: 'org.xbib.gradle.plugin.jbake'

// Generate service documentation site
// To generate clean URLs, add the following settings:
//    configuration['uri.noExtension'] = true
//    configuration['uri.noExtension.prefix'] = '/'
jbake {
    siteHost = findProperty ('siteHost') ?: ''
    srcDirName = findProperty ('siteSource') ?: 'site'
    destDirName = findProperty ('siteTarget') ?: srcDirName // It is relative to `buildDir`
    configData = findProperty ('configData') ?: [:]

    version  = '2.5.1'

    configuration['site.host'] = siteHost

    configuration['render.index'] = file("$projectDir/templates/index.ftl").exists()
    configuration['render.tags'] = false
    configuration['render.archive'] = false
    configuration['render.feed'] = false
    configuration['render.sitemap'] = true

    configuration['markdown.extensions'] = 'ALL,-HARDWRAPS,-AUTOLINKS,-EXTANCHORLINKS'

    // Custom properties
    configuration['projectName'] = rootProject.name // Applied in templates, not content
    configuration['projectDescription'] = rootProject.description
    configuration['projectVersion'] = rootProject.version.toString()
    configuration['projectGroup'] = rootProject.group.toString ()
    configuration['siteHost'] = configuration['site.host']

    // Custom extra data
    configData.each { entry ->
        configuration[entry.key] = entry.value
    }
}

// Site processing is required because JBake does not filter content with configuration properties
task site (dependsOn: 'jbake') {
    doLast {
        file ("$buildDir/$jbake.destDirName").traverse (nameFilter: ~/.+\.(html|xml|css|svg)$/) {
            filterFile (it, jbake.configuration)
        }
    }
}

private static void filterFile (final File file, final Map<?, ?> data) {
    String text = file.getText ()

    data.each { entry ->
        text = text.replace ("\${$entry.key}", entry.value.toString ())
    }

    file.write (text)
}
